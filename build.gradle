plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.2'
}

group = 'com.github.integritymc'
version = 'v0.6'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven { url 'https://jitpack.io' }
}

dependencies {
    //SPIGOT - REPO: PaperMC
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"

    //LOMBOK (FOR GETTERS AND SETTERS) - REPO: Central
    compileOnly "org.projectlombok:lombok:1.18.38"
    annotationProcessor "org.projectlombok:lombok:1.18.38"
    testCompileOnly "org.projectlombok:lombok:1.18.38"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.38"

    //INTEGRITY UPDATER - REPO: Jitpack
    implementation 'com.github.IntegrityMC:IntegrityUpdater:v2.0'

    //BSTATS - REPO: Central
    implementation 'org.bstats:bstats-bukkit:3.0.2'

    //VaultAPI - REPO: Jitpack
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7.1'

    //AdventureAPI - REPO: Central
    implementation "net.kyori:adventure-api:4.24.0"
    implementation "net.kyori:adventure-text-minimessage:4.24.0"
    implementation "net.kyori:adventure-platform-bukkit:4.4.1"

    //SQL Storage
    implementation 'org.xerial:sqlite-jdbc:3.50.3.0'
    implementation 'com.mysql:mysql-connector-j:9.4.0'

    //FOLIA
    implementation 'com.github.Fra-McNaples:FoliaScheduler-API:java17-SNAPSHOT'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

shadowJar {
    archiveFileName = project.name+".jar"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    relocate("net.kyori", "com.github.integritymc.shade.kyori")
    relocate("com.github.integrityupdate", "com.github.integritymc.shade.updater")
    relocate("org.bstats", "com.github.integritymc.shade.bstats")
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
